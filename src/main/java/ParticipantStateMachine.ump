class Participant {
  depend ca.mcgill.ecse.biketourplus.controller.BikeToursFeatureSetController;
  TourStatus { 
    
    NotAssigned { 
      setParticipantTour(BikeTour aBikeTour) / {setBikeTour(aBikeTour);} -> AssignedUnpaid; 
    }
    AssignedUnpaid {
      payForTrip() / {doPayForTrip();} -> Paid;
      startTripForParticipant() -> Banned;
      cancelTripForParticipant() / {setRefund(0);} -> TripComplete;
      
    }
    Paid {
      cancelTripForParticipant() / {setRefund(50);} -> TripComplete;
      startTripForParticipant() -> OnTrip;
      
    }
    OnTrip {
      finishTripForParticipant() / {setRefund(0);} -> TripComplete; 
      cancelTripForParticipant() / {setRefund(10);} -> TripComplete;
    }
    Banned {
    }
    final TripComplete {
    }
  }
  
  private void doPayForTrip() {
    BikeToursFeatureSetController.payForParticipantTrip(this);
  }
  private void setRefund(int percent) {
    setRefundedPercentageAmount(percent);
  }
}